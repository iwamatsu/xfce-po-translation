dnl $Id: configure.in.in 4729 2008-05-05 11:11:37Z mmassonnet $
dnl
dnl xfce4-notes-plugin - Notes applet for the Xfce4 panel
dnl
dnl 2003 Benedikt Meurer <benedikt.meurer@unix-ag.uni-siegen.de>
dnl 2003 Jakob Henriksson <b0kaj+dev@lysator.liu.se>
dnl 2006, 2007 Mike Massonnet <mmassonnet@gmail.com>
dnl

m4_define([xfce4_notes_plugin_version_major], [1])
m4_define([xfce4_notes_plugin_version_minor], [6])
m4_define([xfce4_notes_plugin_version_micro], [3])
m4_define([xfce4_notes_plugin_version_nano], [])
m4_define([xfce4_notes_plugin_version_build], [r@REVISION@])
m4_define([xfce4_notes_plugin_version_tag], [svn]) # Leave empty for releases
m4_define([xfce4_notes_plugin_version], [xfce4_notes_plugin_version_major().xfce4_notes_plugin_version_minor().xfce4_notes_plugin_version_micro()ifelse(xfce4_notes_plugin_version_nano(), [], [], [.xfce4_notes_plugin_version_nano()])ifelse(xfce4_notes_plugin_version_tag(), [svn], [xfce4_notes_plugin_version_tag()-xfce4_notes_plugin_version_build()], [xfce4_notes_plugin_version_tag()])])

AC_INIT([xfce4-notes-plugin], [xfce4_notes_plugin_version], 
        [mmassonnet@gmail.com])

AM_INIT_AUTOMAKE([xfce4-notes-plugin], [xfce4_notes_plugin_version()])
AM_CONFIG_HEADER([config.h])
AM_MAINTAINER_MODE

dnl Avoid g77 and g++ checks
m4_undefine([AC_PROG_CXX])
m4_defun([AC_PROG_CXX],[])
m4_undefine([AC_PROG_F77])
m4_defun([AC_PROG_F77],[])

dnl Check for UNIX variants
AC_AIX
AC_ISC_POSIX
AC_MINIX

dnl Check for basic programs
AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_INTLTOOL

dnl Check for standard header files
AC_HEADER_STDC
AC_CHECK_HEADERS([math.h])

dnl Check for X11 installed
XDT_CHECK_LIBX11_REQUIRE

dnl configure the panel plugin
XDT_CHECK_PACKAGE([GTK], [gtk+-2.0], [2.10.0])
XDT_CHECK_PACKAGE([LIBXFCEGUI4], [libxfcegui4-1.0], [4.4.0])
XDT_CHECK_PACKAGE([LIBXFCE4UTIL], [libxfce4util-1.0], [4.4.0])
XDT_CHECK_PACKAGE([LIBXFCE4PANEL], [libxfce4panel-1.0], [4.4.0])

dnl check for xfconf
m4_define([xfconf_minimum_version], [0])
AC_ARG_ENABLE([configure-dialog],
              [AC_HELP_STRING([--enable-configure-dialog],
                              [Build with configure dialog (xfce >= 4.5)])],
              [xfconf=$enableval],
              [xfconf=no])
if test "x$xfconf" = "xyes"; then
    XDT_CHECK_PACKAGE([XFCONF], [libxfconf-0], [xfconf_minimum_version], [have_xfconf=yes],
                      [
                      echo "*** Optional package libxfconf-0 was either not found on your system"
                      echo "*** or is too old.  Please install or upgrade to at least version"
                      echo "*** xfconf_minimum_version, or adjust the PKG_CONFIG_PATH environment variable"
                      echo "*** if you installed the new version of the package in a nonstandard"
                      echo "*** prefix.  Configuration dialog will be disabled."
                      ])

    if test "x$have_xfconf" = "xyes" ; then
        AC_DEFINE([HAVE_XFCONF], [1], [Define if Xfconf is present])
    fi
fi

dnl check for thunar_vfs
m4_define([thunar_minimum_version], [0.8.0])
AC_ARG_ENABLE([fs-monitor],
              [AC_HELP_STRING([--disable-fs-monitor],
                              [Don't use thunar-vfs to monitor the file system. (default=enabled)])],
              [thunar_vfs=$enableval],
              [thunar_vfs=yes])
if test "x$thunar_vfs" = "xyes" ; then
    XDT_CHECK_PACKAGE([THUNAR_VFS], [thunar-vfs-1], [thunar_minimum_version], [have_thunar_vfs=yes],
                      [
                      echo "*** Optional package thunar-vfs-1 was either not found on your system"
                      echo "*** or is too old.  Please install or upgrade to at least version"
                      echo "*** thunar_minimum_version, or adjust the PKG_CONFIG_PATH environment variable"
                      echo "*** if you installed the new version of the package in a nonstandard"
                      echo "*** prefix.  File system monitoring will be disabled."
                      ])

    if test "x$have_thunar_vfs" = "xyes" ; then
        AC_DEFINE([HAVE_THUNAR_VFS], [1], [Define if thunar-vfs is present.])
    fi
fi

dnl Translations
XDT_I18N([@LINGUAS@])

dnl Check for debugging support
XDT_FEATURE_DEBUG()

AC_OUTPUT([
Makefile
icons/Makefile
icons/16x16/Makefile
icons/22x22/Makefile
icons/24x24/Makefile
icons/32x32/Makefile
icons/scalable/Makefile
panel-plugin/Makefile
po/Makefile.in
])
