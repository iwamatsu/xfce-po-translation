dnl $Id$
dnl
dnl xfmpc - A graphical GTK+ MPD client focusing on low footprint
dnl
dnl 2008      Mike Massonnet <mmassonnet@xfce.org>
dnl

dnl ***************************
dnl *** Version information ***
dnl ***************************
m4_define([xfmpc_version_major], [0])
m4_define([xfmpc_version_minor], [0])
m4_define([xfmpc_version_micro], [8])
m4_define([xfmpc_version_build], [r@REVISION@])
m4_define([xfmpc_version_tag], [svn]) # leave empty for release
m4_define([xfmpc_version], [xfmpc_version_major().xfmpc_version_minor().xfmpc_version_micro()ifelse(xfmpc_version_tag(), [svn], [xfmpc_version_tag()-xfmpc_version_build()], [xfmpc_version_tag()])])

dnl ***************************
dnl *** Initialize autoconf ***
dnl ***************************
AC_COPYRIGHT([Copyright (c) 2008
        The Xfce development team. All rights reserved.])
AC_INIT([xfmpc], [xfmpc_version], [http://bugzilla.xfce.org/], [xfmpc])
AC_PREREQ([2.50])
AC_CANONICAL_TARGET()
AC_REVISION([$Id$])

dnl ***************************
dnl *** Initialize automake ***
dnl ***************************
AM_INIT_AUTOMAKE([1.8 dist-bzip2 tar-ustar])
AM_CONFIG_HEADER([config.h])
AM_MAINTAINER_MODE()

dnl ********************************
dnl *** Check for basic programs ***
dnl ********************************
AC_PROG_CC()
AC_PROG_LD()
AC_PROG_INSTALL()
AC_PROG_INTLTOOL()

dnl **********************************
dnl *** Check for standard headers ***
dnl **********************************
AC_HEADER_STDC()

dnl 
dnl *******************************
dnl *** Check for X11 installed ***
dnl *******************************
XDT_CHECK_LIBX11_REQUIRE()

dnl ******************************
dnl *** Check for i18n support ***
dnl ******************************
XDT_I18N([@LINGUAS@])

dnl ***********************************
dnl *** Check for required packages ***
dnl ***********************************
XDT_CHECK_PACKAGE([GTK], [gtk+-2.0], [2.12.0])
XDT_CHECK_PACKAGE([LIBXFCEGUI4], [libxfcegui4-1.0], [4.4.0])
XDT_CHECK_PACKAGE([LIBXFCE4UTIL], [libxfce4util-1.0], [4.4.0])
XDT_CHECK_PACKAGE([LIBMPD], [libmpd], [0.15.0])

dnl ***********************************
dnl *** Check for debugging support ***
dnl ***********************************
XDT_FEATURE_DEBUG()

dnl *********************************
dnl *** Check for configure flags ***
dnl *********************************
AC_ARG_ENABLE([funky-colors],
	      [AC_HELP_STRING([--enable-funky-colors], [Build with more funky colors])],
	      [enable_funkycolors=$enableval],
	      [enable_funkycolors=no])
if test "x$enable_funkycolors" = "xyes" ; then
	AC_DEFINE([MORE_FUNKY_COLOR_ON_SEARCH_ENTRY], [1], [""])
fi

AC_OUTPUT([
Makefile
src/Makefile
po/Makefile.in
])

dnl ***************************
dnl *** Print configuration ***
dnl ***************************
echo
echo "Build Configuration:"
echo
echo "* Debug Support:    $enable_debug"
echo "* Funky Colors:     $enable_funkycolors"
echo

