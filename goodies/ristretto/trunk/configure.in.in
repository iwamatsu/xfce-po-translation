dnl
dnl Copyright (c) 2006
dnl         The Xfce development team. All rights reserved.
dnl
dnl Originally written for Xfce by Benedikt Meurer <benny@xfce.org>
dnl Modified by Stephan Arts <stephan@xfce.org>
dnl

dnl ***************************
dnl *** Version information ***
dnl ***************************
m4_define([ristretto_version_major], [0])
m4_define([ristretto_version_minor], [0])
m4_define([ristretto_version_micro], [21])
m4_define([ristretto_version_build], [r@REVISION@])
m4_define([ristretto_version_tag], [svn]) # Leave empty for releases
m4_define([ristretto_version], [ristretto_version_major().ristretto_version_minor().ristretto_version_micro()ifelse(ristretto_version_tag(),[],[], [-ifelse(ristretto_version_tag(), [svn], [ristretto_version_tag()-ristretto_version_build()], [ristretto_version_tag()])])])

dnl *******************************************
dnl *** Debugging support for SVN snapshots ***
dnl *******************************************
m4_define([ristretto_debug_default], [ifelse(ristretto_version_tag(), [svn], [full], [minimum])])

AC_COPYRIGHT([Copyright (c) 2006-2007
        The Xfce development team. All rights reserved.

Written for Xfce by Stephan Arts <stephan@xfce.org>.])

AC_INIT([ristretto], [ristretto_version], [])
AC_PREREQ([2.50])

RISTRETTO_VERSION=ristretto_version
AM_INIT_AUTOMAKE([ristretto], [$RISTRETTO_VERSION])
AM_CONFIG_HEADER([config.h])
AM_MAINTAINER_MODE

dnl check for UNIX variants
AC_AIX
AC_ISC_POSIX
AC_MINIX
AM_CONDITIONAL([HAVE_CYGWIN], [test "`uname | grep \"CYGWIN\"`" != ""])


dnl check for standard header files
AC_PROG_CC
AC_PROG_INTLTOOL([0.31], [no-xml])

AC_HEADER_STDC

dnl Check for i18n support
XDT_I18N([@LINGUAS@])

XDT_CHECK_PACKAGE([LIBEXIF], [libexif], [0.6.0])
XDT_CHECK_PACKAGE([GTK], [gtk+-2.0], [2.10.0])
XDT_CHECK_PACKAGE([GLIB], [glib-2.0], [2.12.0])
XDT_CHECK_PACKAGE([GOBJECT], [gobject-2.0], [2.12.0])
XDT_CHECK_PACKAGE([THUNAR_VFS], [thunar-vfs-1], [0.4.0])
XDT_CHECK_PACKAGE([DBUS_GLIB], [dbus-glib-1], [0.34])
XDT_CHECK_PACKAGE([LIBXFCE4UTIL], [libxfce4util-1.0], [4.4.0])
XDT_CHECK_PACKAGE([LIBXFCEGUI4], [libxfcegui4-1.0], [4.4.0])

AC_ARG_ENABLE([xfce-desktop],
              [AC_HELP_STRING([--enable-xfce-desktop],
                              [Build with xfdesktop support (xfce >= 4.5)])],
              [build_xfce_desktop=$enableval],
              [build_xfce_desktop=no])
if test "x$build_xfce_desktop" = "xyes"; then
    echo "--------------------------------------"
    echo "BUILDING WITH XFDESKTOP SUPPORT"
    echo "XFDESKTOP 4.5 or higher required"
    echo ""
    echo "Use --disable-xfce-desktop if you are "
    echo "running xfce older then verion 4.5"
    echo "--------------------------------------"
    XDT_CHECK_PACKAGE([XFCONF], [libxfconf-0], [0])
    AC_DEFINE([HAVE_XFCONF], [1], [Define if XFConf is present])
    AC_DEFINE([WITH_DESKTOP_WALLPAPER], [1], [Define With Desktop wallpaper])
fi


dnl check for debugging support
XDT_FEATURE_DEBUG

AC_OUTPUT([
Makefile
po/Makefile.in
src/Makefile
icons/Makefile
icons/16x16/Makefile
icons/22x22/Makefile
icons/24x24/Makefile
icons/32x32/Makefile
icons/36x36/Makefile
icons/48x48/Makefile
icons/scalable/Makefile
])

echo "----------------------------------------"
echo "$PACKAGE $VERSION"
echo ""
echo "The binary will be installed in $prefix/bin"
echo "----------------------------------------"
echo "Xfce 4.6 wallpaper support: $build_xfce_desktop"
echo ""
echo ""
echo "Configure finished, type 'make' to build."
